10 CLS:CLR:PAL0,1:PAL3,15:PAL2,7
20 SYMBOL[2]65,20,"PODRAZ 5",3,2
30 R6$="AG01":X=INT(RND(1)*8+1)
40 R7$="INPUT"+STR$(X)
50 FORX=1TO4
60 R1$=R1$+STR$(INT(RND(1)*9))
70 R3$=R3$+STR$(INT(RND(1)*9))
80 NEXT:R1$="DCN"+R1$+"P":R3$="DKP"+R3$+"D"
90 FORX=1TO8
100 R2$=R2$+STR$(INT(RND(1)*8)+1)
110 NEXT
120 FORX=1TO3
130 R4$=R4$+STR$(INT(RND(1)*6)+1)
140 R5$=R5$+STR$(INT(RND(1)*6)+1)
150 NEXT
160 R4$="BAKER STREET "+R4$+"/"+CHR$(INT(RND(1)*25)+65)
170 R5$="LINCOLN STREET "+R5$+"/"+CHR$(INT(RND(1)*25)+65)
180 CURSOR5,8:PRINT"MILY PRITELI!"
190 TEMPO7
200 A$="G3R2#G3G3R2+C3G5#G3G5+C3+#D6+D3+C3#A3G3R5G5#G3G5+C3G5#G3G5+C3+#D6+D3+C3#A3G3R5#G5G3F3G3#G3G6D6#G3R2G3F3G3#G3G6R5#G5G3F3G3#G3+C5#A3#G3#A3+C3+D6"
210 K$="O3":CURSOR1,10
220 PRINT"VASIM UKOLEM JE OKRAST NASEHO STAREHO   ZNAMEHO JACKA RAGGERA, JENZE TENTOKRAT  TO NEBUDE TAK JEDNODUCHE. BUDETE MUSET  ZJISTIT NEJEN HESLO JEHO KONTA, ALE I   ZMENIT KOD, KTERY HESLO PO INTERVALECH  MENI."
230 CURSOR26,16:PRINT"HODNE STESTI!!"
240 IFK$="O3"THENK$="O2":GOTO260
250 IFK$="O2"THENK$="O3"
260 MUSICK$,A$
270 FORX=1TO1220
280 GETX$:IFASC(X$)>0THEN300
290 NEXT:GOTO240
300 MUSICSTOP
310 GOSUB"MUS":CURSOR5,8:PRINT"NAZDAR TIME. "
320 CURSOR1,10:PRINT"  DOBRE ZE TE ZASE VIDIM MEZI NAMI.       JAK SE ZDA RAGGER SI ZASE LEHCE         PRIVYDELAVA. ALE PRO TENTO PRIPAD       SE POJISTIL. JEHO HESLO (KONTA NBA      19273R681) SE PO INTERVALECH MENI   "
330 CURSOR1,15:PRINT"  A TO POMOCI KODU ULOZENEM V CENTRAL     COMPUTER. TAM MISTO KODU (NA ADRESE     KTEROU NEZNAME) AG01 ULOZIS KOD         AGN2. DALSI INFORMACE AZ MI ZAVOLAS."
340 CURSOR21,20:PRINT"TVUJ ARTHUR BROWN"
350 GETA$:IFA$=""THEN350
360 CLS:SYMBOL[2]65,20,"PODRAZ 5",3,2
370 IFR6$="AG01"THEN380:ELSE410
380 X$=CHR$(INT(RND(1)*25)+65)+CHR$(INT(RND(1)*25)+65)
390 X$=X$+CHR$(INT(RND(1)*9)+48)+CHR$(INT(RND(1)*9)+48)
400 HO$=X$
410 IFSEK>=5THEN3100
420 CURSOR9,8:PRINT"T - VOLAT TELEFONEM"
430 CURSOR9,10:PRINT"S - PODIVAT SE DO SEZNAMU"
440 GETA$:IFA$="T"THEN510
450 IFA$="S"THEN460:ELSE440
460 CLS:CURSOR7,3:PRINT"SPECIALNI TELEFONNI SEZNAM"
470 RESTORE3410:FORX=7 TO 15
480 READA$:CURSOR3,X:PRINTA$
490 SOUND21+X,5:WAIT52:NEXT
500 GETA$:IFA$=""THEN500:ELSE360
510 CLS:CURSOR4,7:PRINT"ZADEJTE TELEFONNI CISLO:<------>"
520 Z1=28:Z2=34:Z3=7:GOSUB"VSTUP1"
530 WAIT90:TEMPO7:MUSIC"O2D1R6D7R9D1R6D7R9D1R6D7","R0"
540 IFV$="625721"THEN930
550 IFV$="826262"THEN1050
560 IFV$="100183"THEN2890
570 IFV$="812114"THEN1590
580 IFV$="811955"THEN2520
590 IFV$="232966"THEN650
600 IFV$="681157"THEN2710
610 IFV$="683918"THEN1700
620 IFV$="615659"THEN1360
630 USR($3E):CURSOR15,13:PRINT"OBSAZENO!!"
640 GETA$:IFA$=""THEN640:ELSE360
650 CLS:PRINT"$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$"
660 FORX=1TO4
670 CURSOR0,X:PRINT"$":CURSOR39,X:PRINT"$":NEXT:CURSOR0,5
680 PRINT"$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$"
690 SYMBOL35,11,"NATIONAL BANK OF",2,1
700 SYMBOL104,24,"AMERICA",2,1
710 CURSOR1,8:PRINT"PREVOD PENEZ Z KONTA:<------------>"
720 Z1=22:Z2=34:Z3=8:GOSUB"VSTUP2"
730 IFV$="NBA19273R681"THEN750
740 CURSOR11,10:PRINT"NEEXISTUJICI KONTO!":GOTO"SPOJ."
750 CURSOR1,10:PRINT"VLOZTE HESLO:<---->"
760 Z1=14:Z2=18:Z3=10:GOSUB"VSTUP2"
770 IFV$=HO$THEN790
780 CURSOR11,14:PRINT"NEEXISTUJICI HESLO!":SEK=SEK+1:GOTO"SPOJ."
790 CURSOR1,12:PRINT"PREVEST NA KONTO:<------------>"
800 Z1=18:Z2=30:Z3=12:GOSUB"VSTUP2"
810 IFV$="NBB26872R926"THEN840
820 CURSOR1,15:PRINT"NA JAKE KONTO TO PREVADIS!!??"
830 CURSOR26,17:PRINT"BROWN":WAIT700:SEK=SEK+2:GOTO"SPOJ."
840 CLS:SYMBOL[2]17,30,"GRATULUJI!!!",3,2
850 A$="+D5B5A5G5+D5B5A5G5B5G5#F5E5B7R7D5E5#F5G5A5B5A5R5G5A5B5+E5A7R7+D5B5A5G5+D5B5A5G5B5G5#F5E5B5R3R7#F5#G5#A5B5+#C5+#D5+#C5R5B5R0B5#A5B5+D5R5D5E5"
860 B$="G7D5E5G7D5E5G5R0G5D5E5G5R5E5G5B7R6R7E5G5B5R3R6R7A5B5+D5R5A5B5+D7A5B5+D5R0+D5A5B5+D7B5#F5E7R6R7B5#F5E5R3R6R7B5+C5+D5R5B5G5+E7+D5B5+D5+C5G5A5+C7G5A5B7#G5"
870 C$="E5+C7B5#G5B5E5#F5#G5A7D5E5G7D5E5G5R5D5E5G5R0G5E5G5B5R5+C5+D5B7A5G5A5R0A5G5A5G5R3R6R7"
880 CURSOR2,8:PRINT"VYBORNE!"
890 CURSOR2,10:PRINT"TAK SE TI TO PRECE JEN POVEDLO! NYNI   MAS DVE MOZNOSTI: CEKAT NA NOVY PODRAZ  NEBO SI HO NAPSAT SAM."
900 CURSOR18,14:PRINT"PREJI TI MNOHO ZDARU!"
910 TEMPO7:MUSIC"O2",A$,B$,C$,"O3D5E5",B$,C$,"R0"
920 WAIT1000:CLS:PAL0,0:END
930 CLS:CURSOR9,2:PRINT"<<SOUKROMY UZIVATEL>>"
940 CURSOR13,4:PRINT"ARTHUR BROWN"
950 CURSOR1,8:PRINT"VLOZTE HESLO:<---->"
960 Z1=14:Z2=18:Z3=8:GOSUB"VSTUP2"
970 IFV$="SONY"THEN990
980 CURSOR7,11:PRINT"S VAMI SE NEMAM CO BAVIT!!":SOUND38,15:WAIT700:GOTO"SPOJ."
990 CURSOR1,10:GOSUB"MUS"
1000 PRINT"ZE VOLAS TIME! MUSIM TI RICT PAR VECI   OHLEDNE RAGGERA. TEN CHLAP JE REGI-     STROVAN V UNION COMPUTER`S MAN`S. JEHO  KONTO V NATIONAL BANK OF AMERICA UZ"
1010 CURSOR1,14
1020 PRINT"ZNAS, ALE JESTLI SE TI PODARI ZJISTIT   JEHO HESLO ULOZ PENIZE NA KONTO - NBB   26872R926. DALSI INFORMACE DOSTANES     OD AHLA."
1030 CURSOR24,18:PRINT"ARTHUR BROWN"
1040 GETA$:IFA$=""THEN1040:ELSE"SPOJ."
1050 CLS:PRINT"&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&"
1060 FORX=1TO4
1070 CURSOR0,X:PRINT"&":CURSOR39,X:PRINT"&":NEXT:CURSOR0,5
1080 PRINT"&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&"
1090 SYMBOL16,15,"BOSTON`S DATA BANK",2,2
1100 CURSOR5,7:PRINT"1.SPOJENI S JINYMI DATABANKAMI"
1110 CURSOR5,9:PRINT"2.KARTOTEKA OSOBNICH UDAJU"
1120 GETA:IFA=1THEN1140
1130 IFA=2THEN1220:ELSE1120
1140 BOX[0]40,56,280,80,0
1150 CURSOR5,7:PRINT"SPOJENI S JINYMI DATABANKAMI"
1160 CURSOR1,10:PRINT"ZADEJTE NAZEV DATABANKY:"
1170 CURSOR1,12:PRINT"<------------------------>"
1180 Z1=1:Z2=25:Z3=12:GOSUB"VSTUP2"
1190 IFV$="BOSTON`S DATA BANK"THEN1050
1200 IFV$="WASHINGTON`S DATA BANK"THEN1360
1210 CURSOR8,16:PRINT"NEEXISTUJICI DATABANKA!":GOTO"SPOJ."
1220 BOX[0]40,56,280,80,0
1230 CURSOR7,7:PRINT"KARTOTEKA OSOBNICH UDAJU"
1240 CURSOR1,10:PRINT"VLOZTE HESLO:<------>"
1250 Z1=14:Z2=20:Z3=10:GOSUB"VSTUP2"
1260 IFV$=R7$THEN1280
1270 CURSOR12,13:PRINT"HESLO NESOUHLASI":GOTO"SPOJ."
1280 CURSOR1,10:PRINT"VLOZTE REGISTRACNI CISLO:<-------->"
1290 Z1=26:Z2=34:Z3=10:GOSUB"VSTUP2"
1300 IFV$=R1$THEN1320
1310 CURSOR11,13:PRINT"NEEXISTUJICI KARTA":GOTO"SPOJ."
1320 CURSOR2,12:PRINT"JACK RAGGER - 42 LET, 186 CM VYSOKY,    HNEDE VLASY, MODRE OCI. TRVALE BYDLI    V BOSTONU NA ";R4$;"."
1330 GOSUB"MUS":CURSOR2,15
1340 PRINT"POVOLANIM PROGRAMATOR. BYL PRIJAT U     NAS NA POZADANI RICHARDA PARKERA. JE    CLEN SVAZU V UNION COMPUTER`S MAN`S.    TRESTNY REJSTRIK MA VE WASHINGTON`S     DATA BANK NA KARTE ";R3$;"."
1350 GETA$:IFA$=""THEN1350:ELSE"SPOJ."
1360 CLS:PRINT"****************************************"
1370 FORX=1TO4
1380 CURSOR0,X:PRINT"*":CURSOR39,X:PRINT"*":NEXT:CURSOR0,5
1390 PRINT"****************************************"
1400 SYMBOL65,11,"WASHINGTON`S",2,1
1410 SYMBOL90,26,"DATA BANK",2,1
1420 CURSOR5,7:PRINT"1.SPOJENI S JINYMI DATABANKAMI"
1430 CURSOR5,9:PRINT"2.KARTOTEKA OSOBNICH UDAJU"
1440 GETA:IFA=1THEN1140
1450 IFA=2THEN1460:ELSE1440
1460 BOX[0]40,56,280,80,0
1470 CURSOR7,7:PRINT"KARTOTEKA OSOBNICH UDAJU"
1480 CURSOR1,10:PRINT"VLOZTE KOD:<-------->"
1490 Z1=12:Z2=20:Z3=10:GOSUB"VSTUP2"
1500 IFV$=R2$THEN1520
1510 CURSOR15,13:PRINT"NESMYSL!!!":GOTO"SPOJ."
1520 CURSOR1,10:PRINT"ZADEJTE REGISTRACNI CISLO:<-------->"
1530 Z1=27:Z2=35:Z3=10:GOSUB"VSTUP2"
1540 IFV$=R3$THEN1560
1550 CURSOR10,13:PRINT"NEEXISTUJICI OZNACENI":GOTO"SPOJ."
1560 CURSOR2,12:GOSUB"MUS"
1570 PRINT"JACK RAGGER ~ 14 KRAT TRESTANY PRO      PADELANI PROGRAMU A 2 KRAT TRESTANY     PRO POCITACOVE KRADEZE, PRESTO MA       VYBORNY STAV UCTU. NAPOSLEDY BYL        PRIJATY V BOSTON`S DATA BANK. VSTUP     POD HESLEM ";R7$;"."
1580 GETA$:IFA$=""THEN1580:ELSE"SPOJ."
1590 CLS:CURSOR9,2:PRINT"<<SOUKROMY UZIVATEL>>"
1600 CURSOR14,4:PRINT"DAVID AHL"
1610 CURSOR1,8:PRINT"VLOZTE HESLO:<------->"
1620 Z1=14:Z2=21:Z3=8:GOSUB"VSTUP2"
1630 IFV$="TOSHIBA"THEN1650
1640 CURSOR7,11:PRINT"S VAMI SE NEMAM CO BAVIT!":SOUND40,15:WAIT300:GOTO"SPOJ."
1650 CURSOR3,11:GOSUB"MUS"
1660 PRINT"NAZDAR TIME. MAM PRO TEBE DULEZITE      INFORMACE. V UNION COMPUTER`S MAN`S     BUDES MUSET VSTOUPIT DO SPOLKU A"
1670 CURSOR3,14:PRINT"PODSTOUPIT JAKOUSI ZKOUSKU. TAK SI      DEJ POZOR! A JESTE V BOSTON`S DATA      BANK JE RAGGER EVIDOVAN POD CISLEM      ";R1$;"."
1680 CURSOR26,19:PRINT"DAVID AHL"
1690 GETA$:IFA$=""THEN1690:ELSE"SPOJ."
1700 CLS:PRINT"########################################"
1710 FORX=1TO4
1720 CURSOR0,X:PRINT"#":CURSOR39,X:PRINT"#":NEXT:CURSOR0,5
1730 PRINT"########################################"
1740 SYMBOL30,11,"UNION COMPUTER`S",2,1
1750 SYMBOL120,25,"MAN`S",2,1
1760 CURSOR7,7:PRINT"1. NASE INFORMACE"
1770 CURSOR7,9:PRINT"2. PODANI PRIHLASKY DO SVAZU"
1780 GETA:IFA=1THEN1800
1790 IFA=2THEN2130:ELSE1780
1800 BOX[0]56,56,280,80,0
1810 CURSOR8,8:PRINT"JSTE CLENEM SVAZU (A/N)?"
1820 GETA$:IFA$="A"THEN1850
1830 IFA$="N"THEN1840:ELSE1820
1840 CURSOR2,8:PRINT"INFORMACE PODAVAME POUZE SVYM LIDEM":WAIT800:GOTO"SPOJ."
1850 CURSOR1,10:PRINT"VLOZTE CISLO KARTY:<----->"
1860 Z1=20:Z2=25:Z3=10:GOSUB"VSTUP2"
1870 IFV$=CK$THEN1890
1880 CURSOR11,12:PRINT"NEEXISTUJICI KARTA":WAIT600:SEK=SEK+1:GOTO"SPOJ."
1890 CURSOR1,12:PRINT"VLOZTE JMENO:<--------------->"
1900 Z1=14:Z2=29:Z3=12:GOSUB"VSTUP2"
1910 IFV$="TIM COLEMAN"THEN1930
1920 CURSOR15,14:PRINT"NESOUHLASI":WAIT500:SEK=SEK+1:GOTO"SPOJ."
1930 E$="":F$="":FORX=1TO4
1940 K=INT(RND(1)*25)+65
1950 E$=E$+" "+STR$(K)
1960 F$=F$+CHR$(K)
1970 NEXT
1980 CURSOR1,14:PRINT"VLOZTE HESLO PODLE KODU:"E$
1990 CURSOR2,16:PRINT":<---->"
2000 Z1=3:Z2=7:Z3=16:GOSUB"VSTUP2"
2010 IFV$=F$THEN2030
2020 CURSOR16,17:PRINT"NESMYSL":WAIT600:SEK=SEK+1:GOTO"SPOJ."
2030 BOX[0]8,64,296,136,0
2040 CURSOR2,9:PRINT"ZADEJTE JMENO OSOBY O KTERE SE CHCETE"
2050 CURSOR2,10:PRINT"INFORMOVAT:<--------------->"
2060 Z1=13:Z2=28:Z3=10:GOSUB"VSTUP2"
2070 IFV$="JACK RAGGER"THEN2090
2080 CURSOR7,12:PRINT"TAKOVY CLOVEK NENI V SVAZU!":WAIT600:GOTO"SPOJ."
2090 GOSUB"MUS":CURSOR1,9
2100 PRINT"JMENO: JACK RAGGER - PROGRAMATOR V      BOSTON`S DATA BANK. JEHO TRESTNY        REJSTRIK NA KARTE ";R3$;" (WASHIN-     GTON`S DATA BANK S KODEM ";R2$;")"
2110 CURSOR1,13:PRINT"JE SICE DOST PESTRY, ALE JE VELMI       DOBRYM CLENEM NASEHO SPOLKU. MA         STALE STYKY SE SVYM PRITELEM PARKEREM."
2120 GETA$:IFA$=""THEN2120:ELSE"SPOJ."
2130 BOX[0]56,56,280,80,0
2140 CURSOR2,9:PRINT"JMENO:<----------->"
2150 Z1=8:Z2=19:Z3=9:GOSUB"VSTUP2"
2160 CURSOR2,11:PRINT"VEK:<-->"
2170 Z1=6:Z2=8:Z3=11:GOSUB"VSTUP1"
2180 IFVAL(V$)<50 AND VAL(V$)>30 THEN2200
2190 CURSOR9,13:PRINT"NEODPOVIDAJICI VEK!!!":WAIT600:GOTO"SPOJ."
2200 GOSUB"MUS":CURSOR2,13
2210 PRINT"VITAM VAS MEZI NAMI. VIDIM ZE O NAS     SVAZ JE CILY ZAJEM. NYNI VSAK JAKO      VSICHNI MUSITE PODSTOUPIT MALOU         ZKOUSKU - BUDOU VAM ZADAVANA DVE        CISLA A VAS UKOL JE VZDY NAPSAT         JEJICH SOUCIN."
2220 GETA$:IFA$=""THEN2220
2230 BOX[0]16,72,296,152,0
2240 FORN=1TO10
2250 A=INT(RND(1)*7+5):B=INT(RND(1)*6+2)
2260 X$=STR$(A)+" * "+STR$(B)+" = ??  "
2270 D=LEN(STR$(A)+STR$(B))
2280 CURSOR14,11:PRINTX$
2290 Z1=19+D:Z2=Z1+2:Z3=11
2300 V$="":FORX=1TO340
2310 GETA$:IFA$="0" OR VAL(A$)>0 THEN2320:ELSE2340
2320 Z1=Z1+1:CURSORZ1,Z3:PRINTA$
2330 V$=V$+A$:IFZ1=Z2THEN2360
2340 NEXTX
2350 CURSOR13,11:PRINT"CAS VYPRSEL!!":WAIT400:GOTO"SPOJ."
2360 IFV$=STR$(A*B)THEN2380
2370 CURSOR16,14:PRINT"BLBOST":WAIT500:GOTO"SPOJ."
2380 SOUND34,30:WAIT340
2390 NEXTN
2400 GOSUB"MUS":CURSOR2,10
2410 PRINT"VYBORNE PRITELI. OD TETO CHVILE JSTE    CLENEM NASI SPOLECNOSTI. JESTE MOMENT   POCKEJTE NA CISLO VASI KARTY."
2420 CURSOR11,15:PRINT"CISLO KARTY:....."
2430 MUSIC"R0":X$=""
2440 FORN=1TO25
2450 FORX=1TO4
2460 X$=X$+STR$(INT(RND(1)*8)+1)
2470 NEXT:X$=X$+CHR$(INT(RND(1)*25)+65)
2480 CK$=X$:SOUND29,4
2490 CURSOR23,15:PRINTCK$
2500 X$="":WAIT42:NEXT
2510 GETA$:IFA$=""THEN2510:ELSE"SPOJ."
2520 CLS:CURSOR9,2:PRINT"<<SOUKROMY UZIVATEL>>"
2530 CURSOR15,4:PRINT"JACK RAGGER"
2540 CURSOR1,8:PRINT"VLOZTE SVE JMENO:<---------------->"
2550 Z1=18:Z2=34:Z3=8:GOSUB"VSTUP2"
2560 IFV$="RICHARD PARKER"THEN2580
2570 CURSOR10,11:PRINT"JDETE SE VYCPAT":WAIT700:GOTO"SPOJ."
2580 CURSOR1,10:PRINT"ULICE:<-------------------->"
2590 Z1=7:Z2=27:Z3=10:GOSUB"VSTUP2"
2600 IFV$=R5$THEN2620
2610 CURSOR9,13:PRINT"ZAVOLEJ AZ TI BUDE LIP":WAIT700:SEK=SEK+1:GOTO"SPOJ."
2620 GOSUB"MUS":CURSOR2,12
2630 X$="":FORX=1TO3
2640 X$=X$+STR$(INT(RND(1)*9))
2650 NEXT:CA$="PC"+X$
2660 PRINT"CAU RISO. DOUFAM ZE SE MAS DOBRE.       ZDA SE MI, ZE SE NAM NEKDO VRTAL V      SYSTEMU. TAK RADEJI ZMENIM HESLO U"
2670 CURSOR2,15
2680 PRINT"CENTRAL COMPUTER. MISTO DOSAVADNIHO     TAM DAM ~ NPL1883485OR. JESTE TI        MUSIM RICT, ZE NAS KOD JE NA ADRESE     ~ ";CA$;"."
2690 CURSOR29,19:PRINT"RAGGER"
2700 GETA$:IFA$=""THEN2700:ELSE"SPOJ."
2710 CLS:CURSOR9,2:PRINT"<<SOUKROMY UZIVATEL>>"
2720 CURSOR12,4:PRINT"RICHARD PARKER"
2730 CURSOR1,8:PRINT"VLOZTE SVE JMENO:<---------------->"
2740 Z1=18:Z2=34:Z3=8:GOSUB"VSTUP2"
2750 IFV$="JACK RAGGER"THEN2770
2760 CURSOR7,10:PRINT"NEHREJ SI SE MNOU NADHERO!":WAIT700:GOTO"SPOJ."
2770 CURSOR1,10:PRINT"ULICE:<------------------>"
2780 Z1=7:Z2=25:Z3=10:GOSUB"VSTUP2"
2790 IFV$=R4$THEN2810
2800 CURSOR7,12:PRINT"VYSPI SE A ZAVOLEJ JO ?!":WAIT700:SEK=SEK+1:GOTO"SPOJ."
2810 CURSOR1,12:PRINT"VAS VEK:<-->"
2820 Z1=9:Z2=11:Z3=12:GOSUB"VSTUP1"
2830 IFV$="42"THEN2850
2840 CURSOR12,14:PRINT"UZ TE MAM DOST!!":WAIT700:SEK=SEK+1:GOTO"SPOJ."
2850 GOSUB"MUS":CURSOR2,14
2860 PRINT"AHOJ JACKU. DOUFAM, ZE SE MAS DOBRE.    TADY U NAS NA ";R5$;"      JE VSE OK. MAM TAKY NOVEJ COMPUTER      OD IBM. AZ BUDES MIT CAS ZAVOLEJ!
2870 CURSOR29,19:PRINT"PARKER"
2880 GETA$:IFA$=""THEN2880:ELSE"SPOJ."
2890 CLS:PRINT"++++++++++++++++++++++++++++++++++++++++"
2900 FORX=1TO4
2910 CURSOR0,X:PRINT"+":CURSOR39,X:PRINT"+":NEXT:CURSOR0,5
2920 PRINT"++++++++++++++++++++++++++++++++++++++++"
2930 SYMBOL30,13,"CENTRAL COMPUTER",2,3
2940 CURSOR1,8:PRINT"VLOZTE HESLO:<------------>"
2950 Z1=14:Z2=26:Z3=8:GOSUB"VSTUP2"
2960 IFV$="NPL1883485OR"THEN2980
2970 CURSOR12,10:PRINT"NEPLATNE HESLO":WAIT700:GOTO"SPOJ."
2980 CURSOR18,10:PRINT"OK!!"
2990 CURSOR1,12:PRINT"PREJETE SI ZMENIT ADRESU:<----->"
3000 Z1=26:Z2=31:Z3=12:GOSUB"VSTUP2"
3010 IFV$=CA$THEN3030
3020 CURSOR10,14:PRINT"NEEXISTUJICI ADRESA":WAIT700:SEK=SEK+1:GOTO"SPOJ."
3030 CURSOR1,14:PRINT"NA TETO ADRESE JE KOD AG01"
3040 CURSOR1,16:PRINT"ZMENIT NA:<---->"
3050 Z1=11:Z2=15:Z3=16:GOSUB"VSTUP2"
3060 IFV$="AGN2"THENR6$="AGN2":GOTO3080
3070 SEK=SEK+2:GOTO"SPOJ."
3080 CURSOR1,18:PRINT"NAPOSLEDY GENEROVANE HESLO:"HO$
3090 GETA$:IFA$=""THEN3090:ELSE"SPOJ."
3100 CLS:PAL0,2:PAL2,2:PAL1,1
3110 CURSOR8,8:PRINT"TIME COLEMANE!!"
3120 GOSUB"MUS":CURSOR3,10
3130 PRINT"BYL JSTE DOPADEN A NA ZAKLADE           USVEDCUJICICH DUKAZU ODSOUZEN KE        DVEMA A PUL ROKU ODNETI SVOBODY         NEPODMINENE ZA TRESTNY CIN PODVODU"
3140 CURSOR3,14
3150 PRINT"A POKUS O POCITACOVOU KRADEZ.           K VYKONU TRESTU NASTOUPITE ZITRA        VE VEZNICI V SING SINGU..."
3160 GETA$:IFA$=""THEN3160
3170 FORX=0 TO 199
3180 LINE[1]0,X,319,X
3190 WAIT90:NEXT:PAL0,0
3200 CLS:END
3210 LABEL"SPOJ."
3220 FORX=1 TO 20
3230 A$=MID$("SPOJENI PRERUSENO!!!",X,1)
3240 IFASC(A$)<75THENK=25:ELSEK=30
3250 SOUNDK,7:WAIT90:CURSORX+10,21:PRINTA$:NEXT
3260 WAIT550:GOTO360
3270 LABEL"VSTUP1":V$=""
3280 GETA$:IFVAL(A$)>0 OR A$="0"THEN3290:ELSE3280
3290 Z1=Z1+1:CURSORZ1,Z3:PRINTA$
3300 V$=V$+A$:SOUND30,7:WAIT71
3310 IFZ1=Z2THENRETURN:ELSE3280
3320 LABEL"VSTUP2":V$=""
3330 GETA$:IFA$=""THEN3330
3340 IFA$=CHR$(13)THENRETURN
3350 Z1=Z1+1:CURSORZ1,Z3:PRINTA$
3360 V$=V$+A$:SOUND30,7:WAIT71
3370 IFZ1=Z2THENRETURN:ELSE3330
3380 LABEL"MUS"
3390 TEMPO7:MUSIC"O1C1D1E1F1G1A1B1O2C1D1E1F1G1A1B1O3C1D1E1F1G1A1B1O4C1D1E1F1G1A1B1"
3400 RETURN
3410 DATAARTHUR BROWN      (SONY) - 625721
3420 DATABOSTON`S DATA BANK       - 826262
3430 DATACENTRAL COMPUTER         - 100183
3440 DATADAVID AHL      (TOSHIBA) - 812114
3450 DATAJACK RAGGER              - 811955
3460 DATANATIONAL BANK OF AMERICA - 232966
3470 DATARICHARD PARKER           - 681157
3480 DATAUNION COMPUTER`S MAN`S   - 683918
3490 DATAWASHINGTON`S DATA BANK   - 615659

